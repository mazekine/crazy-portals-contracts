// SPDX-License-Identifier: UNLICENSED
pragma ever-solidity >= 0.62.0;

import "../interfaces/board/IJackpot.tsol";
import "./Cashback.tsol";
import "./Ownable.tsol";
import "../libraries/ArrayHelper.tsol";

abstract contract Jackpot is IJackpot, Ownable, Cashback {
    using ArrayHelper for uint[];

    uint16                  constant AVERAGED_PERIODS_DEFAULT = 10;                 //  30 rolls
    uint64                  constant PROBABILITY_FREEZE_PERIOD_DEFAULT = 60;        //  1 minute
    uint64                  constant MIN_PROBABILITY_DENOMINATOR = 10_000_000;      //  0.00001%
    uint64                  constant MAX_PROBABILITY_DENOMINATOR = 100;             //  1%

    uint16                  public jackpotAveragedPeriods = AVERAGED_PERIODS_DEFAULT;
    uint64                  public jackpotProbabilityFreezePeriod = PROBABILITY_FREEZE_PERIOD_DEFAULT;
    uint64                  public jackpotMinProbability = MIN_PROBABILITY_DENOMINATOR;
    uint64                  public jackpotMaxProbability = MAX_PROBABILITY_DENOMINATOR;
    uint64                  public curJackpotProbability = jackpotMaxProbability;
    uint64                         jackpotWinningNumber;
    uint64                         jackpotFreezeProbabilityUntil;

    uint64 latestStepTimestamp;
    uint[] stepIntervals;
    uint64 averageStepInterval;
    uint64 maxAverageInterval;

    constructor() public {
        latestStepTimestamp = Utils._getTimestamp();
        maxAverageInterval = 0;

        //  Generate a first winning number
        rnd.shuffle();
        jackpotWinningNumber = rnd.next(curJackpotProbability);
        jackpotFreezeProbabilityUntil = latestStepTimestamp;
    }

    function setJackpotAveragedPeriods(uint16 qty) external override onlyOwner {
        //tvm.rawReserve(msg.value - 0.05 ever, 12);

        require(qty > 0, BaseErrorCodes.NOT_ALLOWED);
        uint16 oldVal = jackpotAveragedPeriods;
        jackpotAveragedPeriods = qty;
        emit JackpotAveragedPeriodsUpdated(address(this), oldVal, jackpotAveragedPeriods);

        msg.sender.transfer({value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false});
    }

    /**
        Sets the maximum probability of jackpot

        @param  p   The probability denominator, i.e. the lesser it is, the higher the chance of the jackpot will be
    */
    function setJackpotMaxProbability(uint64 p) external override onlyOwner {
        require(p > 0, BaseErrorCodes.DIVISION_BY_ZERO);

        uint64 oldVal = jackpotMaxProbability;
        jackpotMaxProbability = p;
        emit JackpotMaxProbabilityUpdated(address(this), oldVal, jackpotMaxProbability);

        msg.sender.transfer({value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false});
    }

    /**
        Sets the minimum probability of jackpot

        @param  p   The probability denominator, i.e. the greater it is, the lower the chance of the jackpot will be
    */
    function setJackpotMinProbability(uint64 p) external override onlyOwner {
        require(p > 0, BaseErrorCodes.DIVISION_BY_ZERO);
        require(p > jackpotMaxProbability, BaseErrorCodes.NOT_ALLOWED);

        uint64 oldVal = jackpotMinProbability;
        jackpotMinProbability = p;
        emit JackpotMinProbabilityUpdated(address(this), oldVal, jackpotMinProbability);

        msg.sender.transfer({value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false});
    }

    function setJackpotProbabilityFreezePeriod(uint64 period) external override onlyOwner {
        uint64 oldVal = jackpotProbabilityFreezePeriod;
        jackpotProbabilityFreezePeriod = period;
        emit JackpotFreezePeriodUpdated(address(this), oldVal, jackpotProbabilityFreezePeriod);

        msg.sender.transfer({value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false});
    }

    function registerStepInterval(uint64 timestamp) internal {
        uint64 delta = Utils._getTimestamp() - latestStepTimestamp + 1;   //  Add 1 millisecond to avoid potential zero deltas in the same block
        stepIntervals.push(delta);

        latestStepTimestamp = timestamp;

        //  If the averaging window is exceeded, drop the excessive element
        if(jackpotAveragedPeriods < stepIntervals.length) {
            stepIntervals.del(0);
        }

        averageStepInterval = getAverageStepInterval();
        if(averageStepInterval > maxAverageInterval) maxAverageInterval = averageStepInterval;

        updateJackpotProbability();
    }

    function updateJackpotProbability() internal {
        uint64 timestamp = Utils._getTimestamp();

        //  Check that the probability is not frozen
        if(timestamp < jackpotFreezeProbabilityUntil) return;

        //  Until a minimum number of averaged periods is reached, keep the maximum probability
        if(jackpotAveragedPeriods < stepIntervals.length) return;

        //  Get average step interval to assess the board popularity and set a proper probability
        uint64 curInterval = averageStepInterval;
        uint64 p = jackpotMaxProbability + math.muldiv(jackpotMinProbability - jackpotMaxProbability, curInterval, maxAverageInterval);

        //  Limit the minimum probability to avoid overflow and increase fairness of the distribution
        if (p > jackpotMinProbability) p = jackpotMinProbability;

        if (p != curJackpotProbability) {
            //  Update variables and emit event
            uint64 oldVal = curJackpotProbability;
            curJackpotProbability = p;
            emit JackpotProbabilityUpdated(address(this), oldVal, curJackpotProbability);

            //  Generate a new winning number
            rnd.shuffle(uint(curInterval));
            jackpotWinningNumber = rnd.next(curJackpotProbability);

            jackpotFreezeProbabilityUntil = timestamp + jackpotProbabilityFreezePeriod;
        }
    }

    function getAverageStepInterval() internal returns (uint64) {
        if(stepIntervals.empty()) return 0;
        return uint64(stepIntervals.median());
    }

    function drawJackpot(uint64 roundId) internal view returns (bool) {
        //  Take an average of user balance, round Id and average step interval to get a decent random number
        uint randomizer = getRandomizer(roundId);
        rnd.shuffle(randomizer);

        //  Generate a random number and compare it with current winning one
        uint64 playerNumber = rnd.next(curJackpotProbability);
        return (playerNumber == jackpotWinningNumber);
    }

    function getRandomizer(uint64 roundId) internal view returns (uint) {
        return math.divr(uint(address(this).balance) + uint(roundId) + uint(averageStepInterval), 3);
    }

    uint[] something;

    function tryMapSort(uint[] arr) external returns(uint[]) {
        something = arr.mapSort();
        return something;
    }

    function tryQuickSort(uint[] arr) external returns(uint[]) {
        something = arr.quickSort(0, arr.length - 1);
        return something;
    }
}
